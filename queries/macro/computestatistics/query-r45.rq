PREFIX ns: <http://localhost:3030/geographica/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX clc: <http://geo.linkedopendata.gr/corine/ontology#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX geonames: <http://www.geonames.org/ontology#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT ?fClass (COUNT(DISTINCT ?f) AS ?count)
WHERE { 
	GRAPH ns:clc { 
		?clc rdf:type clc:Area. 
		?clc clc:hasLandUse clc:continuousUrbanFabric. 
		?clc clc:hasGeometry ?clcGeo. 
		?clcGeo clc:asWKT ?clcWkt. 
		FILTER(geof:sfIntersects(?clcWkt, "POLYGON ((23.5669247401051 37.9893271592633, 23.5669247401051 38.0366287295546, 23.6743333558464 38.0366287295546, 23.6743333558464 37.9893271592633, 23.5669247401051 37.9893271592633))"^^geo:wktLiteral)). 
	} 
	GRAPH ns:geonames { 
		?f rdf:type geonames:Feature. 
		?f geonames:featureClass ?fClass. 
		?f geonames:hasGeometry ?fGeo. 
		?fGeo geonames:asWKT ?fWkt. 
		FILTER(geof:sfIntersects(?fWkt, "POLYGON ((23.5669247401051 37.9893271592633, 23.5669247401051 38.0366287295546, 23.6743333558464 38.0366287295546, 23.6743333558464 37.9893271592633, 23.5669247401051 37.9893271592633))"^^geo:wktLiteral)). 
	} 
	FILTER(geof:sfIntersects(?clcWkt, ?fWkt)). 
}
GROUP BY ?fClass